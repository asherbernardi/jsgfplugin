plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.18'
    id 'antlr'
}

group 'com.asherbernardi'
version '1.0.1'

patchPluginXml {
    pluginDescription '''
<p>This is a plugin for all JetBrains IDEs to support the JSGF file format.</p>
<ul>
    <li> syntax highlighting
    <li> syntax error checking
    <li> semantic error checking
    <li> go to declaration
    <li> find usages
    <li> automatic renaming
    <li> parenthesis, bracket, brace, and angle bracket matching
    <li> search for symbol includes all declared rule names
    <li> view structure
    <li> code completion
</ul>'''
    changeNotes "Supports newest Intellij update version 2020.1, build 201.6668.121"
    sinceBuild "193.6494.35"
    untilBuild "201.6668.121"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// Include the generated files in the source set
sourceSets.main.java.srcDirs 'src/main/antlr'

dependencies {
    antlr("org.antlr:antlr4:4.8") { // use ANTLR version 4
        exclude group:'com.ibm.icu', module:'icu4j'
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.antlr:antlr4-intellij-adaptor:0.1"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.1'
}

generateGrammarSource {
    arguments += ["-package", "com.asherbernardi.jsgfplugin", "-Xexact-output-dir"]
}

publishPlugin {
    token intellijPublishToken
}