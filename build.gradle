plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.5.2'
}

group 'com.asherbernardi'
version '2.2.0'

patchPluginXml {
    pluginDescription.set('''
<p>This is a plugin for all JetBrains IDEs to support the JSGF file format.</p>
<ul>
    <li> syntax highlighting
    <li> syntax error checking
    <li> semantic error checking
    <li> go to declaration
    <li> find usages
    <li> automatic renaming
    <li> parenthesis, bracket, brace, and angle bracket matching
    <li> search for symbol includes all declared rule names
    <li> view structure
    <li> code completion
    <li> automatic formatting
</ul>''')
    changeNotes.set('''
<ul>
    <li>Support new Idea version 221.*
    <li>Improved parsing and formatting
    <li>Reference resolution for grammar names
    <li>Improved file name resolution for imports
</ul>
''')
    sinceBuild.set("203")
}

sourceCompatibility = 11

repositories {
    mavenCentral()
}

// Include the generated files in the source set
sourceSets.main.java.srcDirs 'src/main/gen'

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

test {
    systemProperty "idea.home.path", "C:/Program Files/JetBrains/IntelliJ IDEA Community Edition 2021.1"
    // Workaround for test failures https://youtrack.jetbrains.com/issue/IDEA-278926?_ga=2.196016364.1950444123.1643221178-2000276811.1643221178#focus=Comments-27-5561012.0-0
    scanForTestClasses false
    include "**/*Test.class"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version.set('2022.1')
    downloadSources = true
}

publishPlugin {
    token.set(intellijPublishToken)
}